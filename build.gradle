allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'cobertura'

  group = 'com.nuaavee.mesos.skybridge'
  version = '0.0.1'
}

subprojects {
  apply plugin: 'java'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  project.ext {
    springBootVersion = '1.2.5.RELEASE'
  }

  repositories {
    mavenCentral()
  }

  cobertura {
    coverageIgnoreTrivial = true
    coverageExcludes = ['.*com.nuaavee.skybridge.bootstrap.SkyBridgeApplication',
                        '.*com.nuaavee.skybridge.core.CoreConfiguration',
                        '.*com.nuaavee.skybridge.core.ApplicationConfig',
                        '.*com.nuaavee.skybridge.events.type.*']
  }
}

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "net.saliman:gradle-cobertura-plugin:2.2.7"
  }
}

// collect cobertura reports from subprojects
def files = subprojects.collect { new File(it.projectDir, '/build/cobertura/cobertura.ser') }
cobertura {
  coverageFormats = [ 'xml', 'html' ]
  coverageMergeDatafiles = files
  coverageSourceDirs = ['bootstrap/src/main/java/',
                        'core/src/main/java/',
                        'events/src/main/java/']
}

test.dependsOn(subprojects.test)

task wrapper(type: Wrapper) {
  gradleVersion = '2.5'
}
